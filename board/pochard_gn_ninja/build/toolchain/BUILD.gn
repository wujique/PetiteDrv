toolchain("armcc") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "$MDK_DIR/ARM/ARMCC/bin/armcc.exe --c99 --apcs=interwork --split_sections --depend $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs = [
      "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o",
    ]
  }
                      
  tool("asm") {
    depfile = "{{output}}.d"
    command = "$MDK_DIR/ARM/ARMCC/bin/armasm.exe --apcs=interwork --xref --depend $depfile {{include_dirs}} {{asmflags}} {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "ASM {{output}}"
    outputs = [
      "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o",
    ]
  }
 
  tool("alink") {
    rspfile = "{{output}}.rsp"
    command = "$MDK_DIR/ARM/ARMCC/bin/armar.exe -r {{output}} --via $rspfile"
    description = "AR {{target_output_name}}{{output_extension}}"
    rspfile_content = "{{inputs}}"
    outputs = [
      "{{target_out_dir}}/{{target_output_name}}{{output_extension}}",
    ]
    default_output_extension = ".a"
    output_prefix = "lib"
  }
 
  tool("link") {
    outfile = "bin/{{target_output_name}}{{output_extension}}"
    rspfile = "$outfile.rsp"
    command = "$MDK_DIR/ARM/ARMCC/bin/armlink.exe {{ldflags}} --strict --summary_stderr --info summarysizes --map --xref --callgraph --symbols --info sizes --info totals --info unused --info veneers --list bin/{{target_output_name}}.map -o $outfile --via $rspfile {{libs}}"
    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    rspfile_content = "{{inputs}}"
    default_output_extension = ".axf"
    outputs = [
      outfile,
    ]
  }

  tool("stamp") {
    command = "cmd.exe /c echo > {{output}}"
    description = "STAMP {{output}}"
  }
 
  tool("copy") {
    command = "cp /y {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}